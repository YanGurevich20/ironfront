set shell := ["bash", "-euo", "pipefail", "-c"]

gcp_project := env_var_or_default("GCP_PROJECT", "ironfront-game")
gcp_region := env_var_or_default("GCP_REGION", "europe-west1")
artifact_repo := env_var_or_default("ARTIFACT_REPO", "ironfront")
image_name := env_var_or_default("IMAGE_NAME", "user-service")

fix:
	pnpm run fix

dev:
	just dev-db
	set -a; source ../infra/.env.dev 2>/dev/null || true; set +a; pnpm run dev

dev-db:
	docker start ironfront-user-service-pg || docker run --name ironfront-user-service-pg -e POSTGRES_USER=user -e POSTGRES_PASSWORD=pass -e POSTGRES_DB=user_service -p 5432:5432 postgres:16

push-image tag=(`git rev-parse --short HEAD` + "-" + `date +%Y%m%d%H%M%S`):
	TAG="{{tag}}"; \
	IMAGE="{{gcp_region}}-docker.pkg.dev/{{gcp_project}}/{{artifact_repo}}/{{image_name}}:${TAG}"; \
	echo "Building and pushing linux/amd64 image $IMAGE"; \
	docker buildx build --platform linux/amd64 -t "$IMAGE" --push .; \
	echo "IMAGE_TAG=$TAG"; \
	echo "IMAGE=$IMAGE"

migrate target:
	just _migrate-{{target}}

_migrate-prod:
	command -v cloud-sql-proxy >/dev/null || { echo "cloud-sql-proxy is required"; exit 1; }; \
	eval "$(pnpm -s tsx src/scripts/resolve_stack_env.ts prod)"; \
	INSTANCE_CONN="${STACK_GCP_PROJECT}:${STACK_GCP_REGION}:${STACK_DB_INSTANCE}"; \
	echo "Starting cloud-sql-proxy for $INSTANCE_CONN"; \
	cloud-sql-proxy --address 127.0.0.1 --port 5432 "$INSTANCE_CONN" >/tmp/cloud-sql-proxy.log 2>&1 & \
	PROXY_PID=$!; \
	cleanup() { kill "$PROXY_PID" >/dev/null 2>&1 || true; }; \
	trap cleanup EXIT; \
	for i in $(seq 1 20); do \
		if nc -z 127.0.0.1 5432 >/dev/null 2>&1; then break; fi; \
		sleep 0.5; \
	done; \
	DATABASE_URL="postgresql://${STACK_DB_USER}:${STACK_DB_PASSWORD_ENCODED}@127.0.0.1:5432/${STACK_DB_NAME}" pnpm run db:push; \
	echo "$STACK_STAGE DB push completed"

_migrate-dev:
	just dev-db
	set -a; source ../infra/.env.dev 2>/dev/null || true; set +a; pnpm run db:push
	
deploy:
	TAG="$(git rev-parse --short HEAD)-$(date +%Y%m%d%H%M%S)"; \
	just push-image "$TAG"; \
	cd ../infra/user-service; \
	pulumi config set user-service-infra:imageTag "$TAG" --stack prod; \
	pulumi up --yes --stack prod; \
	echo "Deployed image tag $TAG to stack prod"
